buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.6.RELEASE"
}

ext {
    easymockVersion = "3.4"
    hamcrestVersion = "1.3"
    assertjVersion = "3.4.1"
    cglibVersion = "2.2.2"
    objenesisVersion = "2.2"
    javassistVersion = "3.20.0-GA"
    junitVersion = "4.12"
    junitRulesVersion = "4.8.2"
    testngVersion = "6.9.10"
    xstreamVersion = "1.4.9"
    mockito1Version = "1.10.19"
    mockito2Version = "2.0.42-beta"
    servletVersion = "2.5"

    gradleScriptDir = "${rootProject.projectDir}/gradle"
}

description = 'PowerMock allows you to unit test code normally regarded as untestable.\n' +
        '        For instance it is possible to mock static methods, remove static initializers, allow mocking without dependency\n' +
        '        injection and more.\n' +
        '        PowerMock works by bytecode manipulation.\n' +
        '        PowerMock also contain some utilities that gives you easier access to an objects internal state.\n' +
        '        PowerMock can be used to test otherwise untestable code and also to achieve a cleaner separation between test\n' +
        '        and production code.'

apply from: "${gradleScriptDir}/version.gradle"
apply from: "${gradleScriptDir}/ide.gradle"

configure(allprojects) { project ->
    repositories {
        mavenCentral()
    }

    group = 'org.powermock'

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        dependencies {
            dependency("org.objenesis:objenesis:${objenesisVersion}")
            dependency("org.javassist:javassist:${javassistVersion}")
            dependency("org.assertj:assertj-core:${assertjVersion}")
            dependency("org.easymock:easymock:${easymockVersion}"){
                exclude group: 'cglib', module: 'cglib-nodep'
            }
            dependency("cglib:cglib-nodep:${cglibVersion}")
            dependency("junit:junit:${junitVersion}") {
                exclude group: 'org.hamcrest', module: 'hamcrest-core'

            }
            dependency("org.hamcrest:hamcrest-core:${hamcrestVersion}")
            dependency("javax.servlet:servlet-api:${servletVersion}")
            dependency("com.thoughtworks.xstream:xstream:${xstreamVersion}")
        }
    }

    apply plugin: "java"

    compileJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
    }

    compileTestJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
        options.compilerArgs += "-parameters"
    }

    tasks.withType(JavaCompile) {
        options.warnings = false
    }

    configurations {
        provided
        mockito1
        mockito2
    }

    sourceSets {
        main {
            compileClasspath = compileClasspath + configurations.provided
        }
        test {
            compileClasspath = compileClasspath + configurations.provided
        }
        mainMockito1 {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/resources']
            output.resourcesDir = 'build/mockito1/classes/main'
            output.classesDir   = 'build/mockito1/classes/main'
            compileClasspath = sourceSets.main.compileClasspath + configurations.mockito1 + configurations.provided
            runtimeClasspath = output + compileClasspath  + configurations.mockito1
            sourceCompatibility = 1.6
            targetCompatibility = 1.6
        }
        testMockito1 {
            java.srcDirs = ['src/test/java']
            resources.srcDirs = ['src/test/resources']
            output.resourcesDir = 'build/mockito1/classes/test'
            output.classesDir   = 'build/mockito1/classes/test'
            compileClasspath = sourceSets.test.compileClasspath + configurations.mockito1 + configurations.provided
            runtimeClasspath = output + compileClasspath  + configurations.mockito1
            sourceCompatibility = 1.6
            targetCompatibility = 1.6
        }
        mainMockito2 {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/resources']
            output.resourcesDir = 'build/mockito2/classes/main'
            output.classesDir   = 'build/mockito2/classes/main'
            compileClasspath = sourceSets.main.compileClasspath + configurations.mockito2 + configurations.provided
            runtimeClasspath = output + compileClasspath  + configurations.mockito2
            sourceCompatibility = 1.6
            targetCompatibility = 1.6
        }
        testMockito2 {
            java.srcDirs = ['src/test']
            resources.srcDirs = ['src/test/resources']
            output.resourcesDir = 'build/mockito2/classes/test'
            output.classesDir   = 'build/mockito2/classes/test'
            compileClasspath = sourceSets.test.compileClasspath + configurations.mockito2 + configurations.provided
            runtimeClasspath = output + compileClasspath  + configurations.mockito2
            sourceCompatibility = 1.6
            targetCompatibility = 1.6
        }
    }

    task mockito1Test(type: Test) {
        description = 'Runs the test with Mockito 1'
        group = 'verification'
        testClassesDir = sourceSets.testMockito1.output.classesDir
        classpath = sourceSets.testMockito1.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }

        reports.junitXml.destination = 'build/mockito1-test-results'
        reports.html.destination = 'build/reports/mockito1Test'
    }

    task mockito2Test(type: Test) {
        description = 'Runs the test with Mockito 2'
        group = 'verification'
        testClassesDir = sourceSets.testMockito2.output.classesDir
        classpath = sourceSets.testMockito2.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }

        reports.junitXml.destination = 'build/mockito2-test-results'
        reports.html.destination = 'build/reports/mockito2Test'
    }

    test{
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }

    mockito1Test.mustRunAfter test
    mockito2Test.mustRunAfter test
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

project("powermock-reflect") {
    description = "Various utilities for accessing internals of a class."

    dependencies {
        compile("org.objenesis:objenesis")
        testCompile("junit:junit")
        testCompile("org.hamcrest:hamcrest-core")
    }

    test{
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }
}

project("powermock-core") {
    description = "Various utilities for accessing internals of a class."

    dependencies {
        compile(project(":powermock-reflect"))
        compile("org.javassist:javassist")
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")
        mockito1("org.mockito:mockito-core:${mockito1Version}")
        mockito2("org.mockito:mockito-core:${mockito2Version}")
    }

    compileTestJava{
        enabled = false
    }

    check.dependsOn testMockito1Classes,testMockito2Classes
}

project("powermock-api:powermock-api-support"){
    description = "PowerMock API Utility classes."

    dependencies {
        compile(project(":powermock-reflect"))
        compile(project(":powermock-core"))
        testCompile("junit:junit")
        testCompile("org.hamcrest:hamcrest-core")
    }
}

project("powermock-api:powermock-api-easymock"){
    description = "PowerMock API Utility classes."

    dependencies {
        compile(project(":powermock-api:powermock-api-support"))
        compile("cglib:cglib-nodep")
        provided("org.easymock:easymock")
        testCompile("junit:junit")
        testCompile("org.hamcrest:hamcrest-core")
    }
}

project("powermock-api:powermock-api-mockito-common"){
    description = "PowerMock API for Mockito. Common classes."

    dependencies {
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")

        mockito1("org.mockito:mockito-core:${mockito1Version}")
        mockito1(project(":powermock-api:powermock-api-support"))

        mockito2("org.mockito:mockito-core:${mockito2Version}")
        mockito2(project(":powermock-api:powermock-api-support"))
    }

    compileJava{
        enabled = false
    }

    compileTestJava{
        enabled = false
    }

    task mockito1Jar(type: Jar) {
        baseName = project.name + "-mockito1"
        dependsOn mainMockito1Classes
        from sourceSets.mainMockito1.output
    }

    task mockito2Jar(type: Jar) {
        baseName = project.name + "-mockito2"
        dependsOn mainMockito2Classes
        from sourceSets.mainMockito2.output
    }

    artifacts {
        mockito1 mockito1Jar
        mockito2 mockito2Jar
    }

    check.dependsOn testMockito1Classes,testMockito2Classes
}

project("powermock-api:powermock-api-mockito"){
    description = "PowerMock API for Mockito 1.+.."

    dependencies {
        compile(project(path:":powermock-api:powermock-api-mockito-common", configuration: "mockito1"))
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")
        provided("org.mockito:mockito-core:${mockito1Version}")
    }
}

project("powermock-api:powermock-api-mockito2"){
    description = "PowerMock API for Mockito 2.+.."

    dependencies {
        compile(project(path:":powermock-api:powermock-api-mockito-common", configuration: "mockito2"))
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")
        provided("org.mockito:mockito-core:${mockito2Version}")
    }
}

project("powermock-tests:powermock-tests-utils"){
    description = "Common set of classes to test PowerMock features in mocking framework modules."

    dependencies {
        compile(project(":powermock-core"))
        compile("javax.servlet:servlet-api")
    }
}

project(":powermock-classloading:powermock-classloading-base"){
    description = "Utilities for loading and executing classes."

    dependencies {
        compile(project(":powermock-api:powermock-api-support"))
        compile(project(":powermock-reflect"))
        compile(project(":powermock-core"))
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")
    }
}


project(":powermock-classloading:powermock-classloading-objenesis"){
    description = "Performs classloader deep-cloning using Objenesis."

    dependencies {
        compile(project(":powermock-classloading:powermock-classloading-base"))
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")
    }
}

project(":powermock-classloading:powermock-classloading-xstream"){
    description = "Performs classloader deep-cloning using X-Stream."

    dependencies {
        compile(project(":powermock-classloading:powermock-classloading-base"))
        compile("com.thoughtworks.xstream:xstream")
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
        testCompile("org.hamcrest:hamcrest-core")
    }
}